plugins {
    alias libs.plugins.android.application
    alias libs.plugins.kotlin.android
    alias libs.plugins.kotlin.kapt
    alias libs.plugins.dagger.hilt
    alias libs.plugins.google.services
    alias libs.plugins.google.crashlytics
}

android {
    namespace 'co.uk.basedapps.vpn'

    defaultConfig {
        applicationId "co.sentinel.dvpnapp"

        minSdk libs.versions.minSdk.get().toInteger()
        targetSdk libs.versions.compileSdk.get().toInteger()
        compileSdk libs.versions.compileSdk.get().toInteger()

        versionCode 50
        versionName "1.0.50"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def versionName = variant.versionName
            outputFileName = "${applicationId}_${versionName}.apk"
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix(".dev")
            buildConfigField "String", "API_URL", "\"https://api.getdvpn.app\""
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "API_URL", "\"https://api.getdvpn.app\""
        }
    }
    compileOptions {
        sourceCompatibility libs.versions.targetJvm.get()
        targetCompatibility libs.versions.targetJvm.get()
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        compose true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }
    packagingOptions {
        resources.excludes.add("META-INF/*")
        // This fix aab issue
        jniLibs.useLegacyPackaging = true
        // Fix building aab (bitcoinj issue)
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
    }
    sourceSets {
        // v2ray libs
        main { jniLibs.srcDirs = ['libs'] }
    }
}

dependencies {

    implementation libs.android.core.ktx
    implementation libs.kotlin.stdlib
    implementation libs.coroutines.core
    implementation libs.timber
    implementation libs.dagger.hilt
    implementation libs.hilt.navigation.compose
    implementation libs.lifecycle.runtime
    implementation libs.bundles.compose
    implementation libs.navigation
    implementation libs.accompanist.systemuicontroller
    implementation libs.webkit
    implementation platform(libs.google.firebase.bom)
    implementation libs.google.firebase.crashlytics
    implementation libs.google.firebase.analytics

    kapt libs.hilt.compiler

    implementation(name: "libv2ray", ext: "aar")
    implementation project(path: ':ui_server')
}
